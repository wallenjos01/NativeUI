// Loom documentation: https://fabricmc.net/wiki/documentation:fabric_loom
// fabric.mod.json documentation: https://fabricmc.net/wiki/documentation:fabric_mod_json

plugins {

    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '7.1.2'

}

configurations {
    shade
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

shadowJar {

    archiveClassifier.set('dev')
    configurations = [project.configurations.shade]
}
tasks.assemble.dependsOn tasks.shadowJar

repositories {

    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {

    minecraft "com.mojang:minecraft:${project.fabric_minecraft_version}"
    mappings loom.officialMojangMappings()
    
    modApi "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi 'org.wallentines:midnightlib:1.0-SNAPSHOT'

    modImplementation 'org.wallentines.midnightcore:api:1.0-SNAPSHOT'
    modImplementation 'org.wallentines.midnightcore:common:1.0-SNAPSHOT'
    modImplementation 'org.wallentines.midnightcore:client:1.0-SNAPSHOT'
    modImplementation 'org.wallentines.midnightcore:fabric:1.0-SNAPSHOT'

    include(modImplementation(fabricApi.module('fabric-resource-loader-v0', "${project.fabric_api_version}")))

    implementation project(':api')
    implementation project(':common')

    shade project(':api')
    shade project(':common')

}

loom {
    runs {
        client {
            runDir = "run/client"
            ideConfigGenerated = false
            client()
        }
        server {
            runDir = "run/server"
            ideConfigGenerated = false
            server()
        }
    }
    interfaceInjection {
		enableDependencyInterfaceInjection = true
	}
}

processResources {

    Map<String, ?> ex = new HashMap<>()
    ex.put("version", project.version)
    ex.put("id", project.mod_id)
    ex.put("name", project.mod_name)
    ex.put("target", project.name)
    ex.put("description", project.description)
    ex.put("group", project.group)
    ex.put("minecraftVersion", project.fabric_minecraft_version)
    ex.put("fabricLoader", project.fabric_loader_version)
    ex.put("mainClass", project.main_class_name)

    filesMatching("fabric.mod.json") {
        expand(ex)
    }
}

remapJar {
    dependsOn(shadowJar)
    inputFile.set(shadowJar.archiveFile)
}